buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    // gradle plugin for scalatest (이 것이 없으면 FunSuite 로 된 것은 테스트를 못한다)
    dependencies {
        classpath 'com.github.maiflai:gradle-scalatest:0.4'
    }
}

subprojects {
    apply plugin: 'scala'
    apply plugin: 'war'
    apply plugin: 'maven'
    apply plugin: 'scalatest'


    group = 'com.scalatra.experiments'
    version = '1.0.0-SNAPSHOT'

    ext {
        jdkVersion = '1.7'
        scalaVersion = '2.11.1'
        scalaMainVersion = '2.11'
        scalatraVersion = '2.3.0.RC3'

        slf4jVersion = '1.7.7'
        jacksonVersion = '2.4.0-rc3'
    }

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion

    sourceSets {
        main {
            scala.srcDir "src/main/java"
            java.srcDirs = []
        }
        test {
            scala.srcDir "src/test/java"
            java.srcDirs = []
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.fork = true
        options.forkOptions.setMemoryMaximumSize("2G")
    }

    tasks.withType(ScalaCompile) {
        options.encoding = 'UTF-8'
        options.fork = true
        options.forkOptions.setMemoryMaximumSize("2G")
        scalaCompileOptions.additionalParameters = ['-target:jvm-1.7']

        configure(scalaCompileOptions.forkOptions) {
            memoryMaximumSize = '1g'
            jvmArgs = ['-ea', '-server', '-XX:MaxPermSize=512m']
        }
    }

    dependencies {

        compile "org.scala-lang:scala-compiler:$scalaVersion"
        compile "org.scala-lang:scala-library:$scalaVersion"

        compile "ch.qos.logback:logback-classic:1.1.2"
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testCompile "org.scalatest:scalatest_$scalaMainVersion:2.1.7"
        testCompile "com.github.axel22:scalameter_$scalaMainVersion:0.5-M2"
        testRuntime 'org.pegdown:pegdown:1.4.2'
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()

        maven { url "http://oss.sonatype.org/content/repositories/releases/" }
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.typesafe.com/typesafe/releases/" }
        maven { url "http://repo.akka.io/repository" }
        maven { url "http://dl.bintray.com/etaty/maven" }
        maven { url "http://scala-tools.org/repo-releases" }
    }
}